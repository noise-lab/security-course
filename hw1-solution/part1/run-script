#!/bin/csh
# 
# To execute this script, see ~cos126/assignments/hello/run-script

unalias *

limit cpu 100 seconds
limit core 0

set RELATIVE_PATH    = "`dirname $0`" #relative
set RUNSCRIPT_PATH   = "`( cd $RELATIVE_PATH && pwd)`" #absolutized and normalized
set GRADEROOT        = `dirname $RUNSCRIPT_PATH`
source $GRADEROOT/bin/setEnvironmentVariables



# test data
set ASSIGNMENT_NAME  = "HW1"
setenv REQUIRED        "PRGen StreamCipher AuthEncryptor AuthDecryptor"
setenv EXTRA_CREDIT    ""
setenv FILES_TO_COPY   "*.txt"

/bin/rm -rf  $OUTPUT $WORKSPACE   >>& /dev/null
mkdir        $OUTPUT $WORKSPACE
chmod 2770   $OUTPUT $WORKSPACE


# do for each name in current directory
foreach name ($argv)
    echo -n $name
    echo -n ": "

    # skip if directory doesn't exist
    set files  = `ls $name/*  |& grep -v "No such file or directory" |& grep -v "No match"`
    if ("$files" == "") then
        echo "no files submitted"
        continue
    endif

    cat < /dev/null > $OUTPUT/$name

    $SCRIPTS/listfiles.scr  $name
    
    $SCRIPTS/compile.scr    $name
    $SCRIPTS/checkstyle.scr $name
    $SCRIPTS/findbugs.scr   $name
    $SCRIPTS/api.scr        $name


    $SCRIPTS/recompileGrader.scr $name    
    $SCRIPTS/execute.scr    $name PRGen
    
    cp -f $GRADEROOT/HW1/solution/PRGen.java $WORKSPACE/$name
    $SCRIPTS/compile.scr    $name
    setenv PRINT_TEXT "executing StreamCipher with reference PRGen"
    setenv ECHO_TEXT ""
    $SCRIPTS/execute.scr    $name StreamCipher

    cp -f $GRADEROOT/HW1/solution/StreamCipher.java $WORKSPACE/$name
    $SCRIPTS/compile.scr    $name
    setenv PRINT_TEXT "executing AuthEncryptor and AuthDecryptor with reference PRGen and reference StreamCipher"
    setenv ECHO_TEXT ""

    $SCRIPTS/execute.scr    $name AuthEncryptor AuthDecryptor    
    unsetenv PRINT_TEXT
    unsetenv ECHO_TEXT
    
    $SCRIPTS/postProcess.scr $name $REQUIRED    
    $SCRIPTS/print.scr      $name

    echo "done."

end

# $SCRIPTS/tar.scr $ASSIGNMENT_NAME
